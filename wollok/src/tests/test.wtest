import main.instrumentos.*
import main.musicos.*

describe "guitarra" {
	test "una Fender está afinada" {
		assert.that(guitarra.estaAfinada())
	}
	test "el costo de una Fender negra es 15" {
		guitarra.color("negra")
		assert.equals(15,guitarra.costo())
	}
	
	test "el costo de una Fender roja es 10" {
		guitarra.color("rojo")
		assert.equals(10,guitarra.costo())	
	}
	
	test "una Fender es valiosa" {
		assert.that(guitarra.esValiosa())
	}

}

describe "trompeta"{
	test "Una trompeta Júpiter a una temperatura ambiente de 20 grados está afinada"{
		trompeta.temperaturaAmbiente(20)
		assert.that(trompeta.estaAfinada())
	}

	test "Una trompeta Júpiter a una temperatura ambiente de 25 grados está afinada" {
		trompeta.temperaturaAmbiente(25)
		assert.that(trompeta.estaAfinada())
	}
	
	test "Una trompeta Júpiter a una temperatura ambiente de 19 grados no está afinada" {
		trompeta.temperaturaAmbiente(19)
		assert.notThat(trompeta.estaAfinada())
	}
	
	test "Una trompeta Júpiter a una temperatura ambiente de 26 grados no está afinada" {
		trompeta.temperaturaAmbiente(26)
		assert.notThat(trompeta.estaAfinada())
	}

	test "Una trompeta Júpiter a una temperatura ambiente de 19 grados, si la afinamos, pasa a estar afinada" {
		trompeta.temperaturaAmbiente(19)
		trompeta.afinar(4)
		assert.that(trompeta.estaAfinada())
	}

	test "La trompeta cuesta sin sordina 30 chelines" {
		trompeta.tieneSordina(false)
		assert.equals(30,trompeta.costo())
	}

	test "La trompeta cuesta con sordina 35 chelines" {
		trompeta.tieneSordina(true)
		assert.equals(35,trompeta.costo())
	}

	test "La trompeta no es valiosa" {
		assert.notThat(trompeta.esValiosa())
	}
}

describe "piano"{
	test "El piano en una habitación de 5x5 está afinado" {
		piano.anchoHabitacion(5)
		piano.largoHabitacion(5)
		assert.that(piano.estaAfinada())
	}
	
	test "El piano en una habitación chica (de 20 metros cuadrados) no está afinado" {
		piano.anchoHabitacion(4)
		piano.largoHabitacion(5) 
		assert.notThat(piano.estaAfinada())
	}

	test "El costo del piano en la habitación de 5x5 es de 10" {
		piano.anchoHabitacion(5)
		piano.largoHabitacion(5)
		assert.equals(10,piano.costo())
	}

	test "El piano es valioso en la habitación inicial de 5x5" {
		piano.anchoHabitacion(5)
		piano.largoHabitacion(5)
		assert.that(piano.esValiosa())
	}
	
	test "El piano movido a una habitación chica (de 20 metros cuadrados) no es valiosa"{
		piano.anchoHabitacion(4)
		piano.largoHabitacion(5) 
		assert.notThat(piano.esValiosa())
	}
}

describe "violin" {
	test "El violín Stagg comienza afinado (0 trémolos)" {
		violin.cantidadTremolos(0)
		assert.that(violin.estaAfinada())
	}

	test "El violín con 9 trémolos todavía está afinado"{
		violin.cantidadTremolos(9)
		assert.that(violin.estaAfinada())
	}

	test "El violín luego de 10 trémolos exactos, queda desafinado" {
		violin.cantidadTremolos(10)
		assert.that(!violin.estaAfinada())
	}

	test "El violín tiene un costo inicial de 20 chelines (sin trémolos)" {
		violin.cantidadTremolos(0)
		assert.equals(20,violin.costo())
	}

	test "Con exactamente 2 trémolos, el violín tiene un costo de 18 chelines" {
        violin.cantidadTremolos(2)
        assert.equals(18, violin.costo())
    }

    test "Con 10 trémolos, el violín tiene un costo de 15 chelines (nunca baja de 15)" {
        violin.cantidadTremolos(10)
        assert.equals(15, violin.costo())
    }

    test "Pintado con laca acrílica, el violín es valioso" {
        violin.pinturaLaqueado("laca acrilica")
        assert.that(violin.esValiosa())
    }

    test "Pintado con látex satinado, el violín no es valioso" {
        violin.pinturaLaqueado("latex satinado")
        assert.that(!violin.esValiosa())
    }
}

describe "johann" {
	test "Johann es feliz con una trompeta Jupiter (que es cara)"{
		assert.that(johann.esFeliz())
	}

	test "Johann no es feliz con un violín Stagg (sin trémolos, que no es cara)"{
		johann.instrumento(violin)
		assert.notThat(johann.esFeliz())
	}
}

describe "wolfgang"{
	test "Wolfgang es feliz si Johann tiene una trompeta Jupiter"{
		johann.instrumento(trompeta)
		assert.that(wolfgang.esFeliz())
	}
	test "Wolfgang no es feliz si Johann tiene un violín Stagg (sin trémolos)"{
		johann.instrumento(violin)
		violin.cantidadTremolos(0)
		assert.notThat(wolfgang.esFeliz())
	}
}

describe "giuseppe"{
	test "Giuseppe es feliz con una guitarra Fender (que está siempre afinada)"{
		giuseppe.instrumento(guitarra)
		assert.that(giuseppe.esFeliz())
	}

	test "Giuseppe no es feliz con un piano Bechstein en una habitación de un metro cuadrado"{
		giuseppe.instrumento(piano)
		piano.anchoHabitacion(1)
		piano.largoHabitacion(1)
		assert.notThat(giuseppe.esFeliz())
	}

}

describe "maddalena"{
	test "Maddalena con el violín Stagg sin trémolos es feliz (costo 20, es par)"{
		maddalena.instrumento(violin)
		violin.cantidadTremolos(0)
		assert.that(maddalena.esFeliz())
	}
	test "Maddalena con el violín Stagg con un trémolo no es feliz (costo 19, es impar)"{
		maddalena.instrumento(violin)
		violin.cantidadTremolos(1)
		assert.notThat(maddalena.esFeliz())
	}
}

describe "asociacionMusical"{
	test "Si tenemos una asociación donde están Johann con un violín Stagg, Wolfgang, 
		Giuseppe con su guitarra Fender original y Maddalena con su violín Stagg original, 
		los músicos felices deben ser Giuseppe y Maddalena"{
		johann.instrumento(violin)
		giuseppe.instrumento(guitarra)
		maddalena.instrumento(violin)
		assert.equals(#{giuseppe,maddalena},asociacionMusical.musicosFelices())
	}
}



